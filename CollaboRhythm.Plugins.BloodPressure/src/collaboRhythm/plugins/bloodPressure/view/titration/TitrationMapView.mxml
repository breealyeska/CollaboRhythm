<?xml version="1.0"?>
<!---
 - Copyright 2013 John Moore, Scott Gilroy
 -
 - This file is part of CollaboRhythm.
 -
 - CollaboRhythm is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
 - License as published by the Free Software Foundation, either version 2 of the License, or (at your option) any later
 - version.
 -
 - CollaboRhythm is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 - warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 - details.
 -
 - You should have received a copy of the GNU General Public License along with CollaboRhythm.  If not, see
 - <http://www.gnu.org/licenses/>.
-->
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 creationComplete="creationCompleteHandler(event)"
		 resize="resizeHandler(event)">
	<fx:Style>
		.extremesLabels {
			font-size: 36;
			color: #939598;
		}
	</fx:Style>
	<fx:Script><![CDATA[
		import collaboRhythm.plugins.bloodPressure.controller.titration.ITitrationMapController;
		import collaboRhythm.plugins.bloodPressure.model.titration.HypertensionMedicationActionSynchronizationDetails;
		import collaboRhythm.plugins.bloodPressure.model.titration.HypertensionMedicationAlternatePair;
		import collaboRhythm.plugins.bloodPressure.model.titration.HypertensionMedicationTitrationModel;

		import mx.core.IVisualElement;
		import mx.events.FlexEvent;
		import mx.events.ResizeEvent;

		private static const MEDICATION_PAIRS_GAP_MIN:int = 5;
		private static const MEDICATION_PAIRS_GAP_MAX:int = 140;

		private static const PADDING:int = 30;

		[Bindable]
		private var _controller:ITitrationMapController;
		[Bindable]
		private var _model:HypertensionMedicationTitrationModel;

		public function set controller(value:ITitrationMapController):void
		{
			_controller = value;
			_model = _controller.model;
			createAlternatePairs();
		}

		override protected function createChildren():void
		{
			super.createChildren();
			createAlternatePairs();
		}

		private function creationCompleteHandler(event:FlexEvent):void
		{
			createAlternatePairs();
		}

		private function createHypertensionMedicationAlternatePairView(hypertensionMedicationAlternatePair:HypertensionMedicationAlternatePair,
																	   nextPair:HypertensionMedicationAlternatePair):void
		{
			var hypertensionMedicationAlternatePairView:HypertensionMedicationAlternatePairView = new HypertensionMedicationAlternatePairView();
			hypertensionMedicationAlternatePairView.init(hypertensionMedicationAlternatePair,
					hypertensionMedicationsVGroup.gap, nextPair);
			addEventListener(HypertensionMedicationDoseSelectionEvent.DOSE_SELECTED,
					hypertensionMedicationViewEvent_doseSelectedHandler);
			addEventListener(HypertensionMedicationAlternateSelectionEvent.ALTERNATE_SELECTED,
					hypertensionMedicationViewEvent_alternateSelectedHandler);

			hypertensionMedicationsVGroup.addElement(hypertensionMedicationAlternatePairView);
		}

		private function hypertensionMedicationViewEvent_doseSelectedHandler(event:HypertensionMedicationDoseSelectionEvent):void
		{
			var hypertensionMedicationActionSynchronizationDetails:HypertensionMedicationActionSynchronizationDetails = createHypertensionMedicationActionSynchronizationDetails(event.altKey,
					event.ctrlKey);
			hypertensionMedicationActionSynchronizationDetails.hypertensionMedicationClass = event.hypertensionMedication.medicationClass;
			hypertensionMedicationActionSynchronizationDetails.doseSelected = event.doseSelected;
			_controller.handleHypertensionMedicationDoseSelected(hypertensionMedicationActionSynchronizationDetails);
		}

		private function hypertensionMedicationViewEvent_alternateSelectedHandler(event:HypertensionMedicationAlternateSelectionEvent):void
		{
			var hypertensionMedicationActionSynchronizationDetails:HypertensionMedicationActionSynchronizationDetails = createHypertensionMedicationActionSynchronizationDetails(event.altKey,
					event.ctrlKey);
			hypertensionMedicationActionSynchronizationDetails.hypertensionMedicationClass = event.hypertensionMedicationAlternatePair.primaryHypertensionMedication.medicationClass;
			_controller.handleHypertensionMedicationAlternateSelected(hypertensionMedicationActionSynchronizationDetails);
		}

		private function createHypertensionMedicationActionSynchronizationDetails(altKey:Boolean,
																				  ctrlKey:Boolean):HypertensionMedicationActionSynchronizationDetails
		{
			var hypertensionMedicationActionSynchronizationDetails:HypertensionMedicationActionSynchronizationDetails = new HypertensionMedicationActionSynchronizationDetails();
			hypertensionMedicationActionSynchronizationDetails.altKey = altKey;
			hypertensionMedicationActionSynchronizationDetails.ctrlKey = ctrlKey;
			hypertensionMedicationActionSynchronizationDetails.selectionByAccountId = _model.activeAccount.accountId;

			return hypertensionMedicationActionSynchronizationDetails;
		}

		private function saveButton_clickHandler(event:MouseEvent):void
		{
			_controller.save();
		}

		private function createAlternatePairs():void
		{
			if (_model && hypertensionMedicationsVGroup.numElements == 0)
			{
				for (var i:int = 0; i < _model.hypertensionMedicationAlternatePairsVector.length; i++)
				{
					var hypertensionMedicationAlternatePair:HypertensionMedicationAlternatePair =
							_model.hypertensionMedicationAlternatePairsVector[i];
					var nextPair:HypertensionMedicationAlternatePair = i + 1 <
							_model.hypertensionMedicationAlternatePairsVector.length ?
							_model.hypertensionMedicationAlternatePairsVector[i + 1] : null;
					createHypertensionMedicationAlternatePairView(hypertensionMedicationAlternatePair, nextPair);
				}
			}
		}

		private function resizeHandler(event:ResizeEvent):void
		{
			updateLayout();
		}

		override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
		{
			super.updateDisplayList(unscaledWidth, unscaledHeight);
			updateLayout();
		}

		override protected function childrenCreated():void
		{
			super.childrenCreated();
			updateLayout();
		}

		private function updateLayout():void
		{
			if (hypertensionMedicationsVGroup && hypertensionMedicationsVGroup.numElements > 0 && parent)
			{
				var numPairs:int = hypertensionMedicationsVGroup.numElements;
				var firstPairView:IVisualElement = hypertensionMedicationsVGroup.getElementAt(0);
				var gap:Number = (height - (numPairs * firstPairView.height) - startLabelGroup.height - (2 * PADDING)) /
						(numPairs - 1);
				gap = Math.max(MEDICATION_PAIRS_GAP_MIN, Math.min(gap, MEDICATION_PAIRS_GAP_MAX));
				hypertensionMedicationsVGroup.gap = gap;

				for (var i:int = 0; i < numPairs; i++)
				{
					(hypertensionMedicationsVGroup.getElementAt(i) as
							HypertensionMedicationAlternatePairView).nextPairGap = gap;
				}
			}
		}
		]]>
	</fx:Script>
	<s:Rect width="100%" height="100%">
		<s:fill>
			<s:SolidColor color="0xE6E7E8"/>
		</s:fill>
	</s:Rect>
	<s:Group left="{PADDING}" top="{PADDING}">
		<s:Label text="Max" styleName="extremesLabels" x="{HypertensionMedicationView.MEDICATION_LABELS_GROUP_WIDTH + HypertensionMedicationView.MEDICATION_HGROUP_GAP}"
				 mouseEnabled="false"/>
		<s:VGroup>
			<s:VGroup id="hypertensionMedicationsVGroup" gap="{MEDICATION_PAIRS_GAP_MIN}">
				<!-- medications get added here dynamically -->
			</s:VGroup>
			<s:Group id="startLabelGroup">
				<s:Label text="Start" styleName="extremesLabels" x="{HypertensionMedicationView.MEDICATION_LABELS_GROUP_WIDTH  + HypertensionMedicationView.MEDICATION_HGROUP_GAP}"
						 mouseEnabled="false"/>
			</s:Group>
		</s:VGroup>
	</s:Group>
</s:Group>
